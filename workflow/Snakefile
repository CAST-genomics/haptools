import warnings
from pathlib import Path
import snakemake.io as io
from snakemake.utils import min_version

##### set minimum snakemake version #####
min_version("6.5.3")


# IMPORT CONFIG VARIABLES
configfile: "config/config.yml"

def check_config(value, default=False, place=config):
    """ return true if config value exists and is true """
    return place[value] if (value in place and place[value]) else default

# handle defaults
config['out'] = check_config('out', 'out')
config['superpop'] = check_config('superpop', 'EUR')
config['min_maf'] = check_config('min_maf', 0)
# remove any trailing slashes in directories and set the variables
out = str(Path(config['out']))
logs = out+"/logs"
bench = out+"/bench"

def get_vcf_chrs(vcf_path):
    """ return a dict of VCFs split by contig """
    if io.contains_wildcard(vcf_path):
        return glob_wildcards(vcf_path).chr
    return []

chrs = get_vcf_chrs(config['snp_vcf'])



rule all:
    input:
        expand(
            out+"/{locus}/merged.tsv.gz",
            locus=[loc.replace(':', '_') for loc in config['loci']]
        )

rule vcf2gt:
    """ convert a VCF into a genotype matrix (cols are vars and rows are samples) """
    input:
        vcf = lambda wildcards: config[wildcards.var+'_vcf'],
        vcf_idx = lambda wildcards: config[wildcards.var+'_vcf']+".tbi",
        samps = config['samples']
    params:
        popn = config['superpop'],
        locus = lambda wildcards: ('chr' if wildcards.var == 'str' else '')+\
            wildcards.chr+":"+wildcards.locus,
        maf = config['min_maf']
    output:
        matrix = out+"/{chr}_{locus}/{var}_matrix.tsv.gz"
    log: logs+"/{chr}_{locus}/{var}/vcf2gt"
    conda: "envs/default.yml"
    shell:
        "workflow/scripts/{wildcards.var}_gt_matrix.bash {params.popn} {params.maf} '{params.locus}' "
        "{input.vcf} {input.samps} 2>{log} | gzip > {output.matrix}"

rule str2gt:
    """ sum the difference of each allele length from the reference allele length """
    input:
        matrix = expand(rules.vcf2gt.output, chr='{chr}', locus='{locus}', var='str')
    output:
        matrix = out+"/{chr}_{locus}/str_gt_matrix.tsv.gz"
    log: logs+"/{chr}_{locus}/str2gt"
    conda: "envs/default.yml"
    shell:
        "workflow/scripts/str_gt.py -o {output.matrix} {input.matrix} 2>{log}"

rule merge_vars:
    """ merge variant files """
    input:
        snp_mat = expand(rules.vcf2gt.output, chr='{chr}', locus='{locus}', var='snp'),
        str_mat = rules.str2gt.output.matrix,
    output:
        mat = out+"/{chr}_{locus}/merged.tsv.gz"
    log: logs+"/{chr}_{locus}/merged"
    conda: "envs/default.yml"
    shell:
        "sort -m -n -k1,1 -t, <(zcat {input.snp_mat}) <(zcat {input.str_mat} | tail -n+2) "
        "2>{log} | cut -f2- -d, | gzip > {output.mat}"

